;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*               PLACA DE APRENDIZAGEM: USTART FOR PIC		   *
;*		 PROGRAMAÇÃO EM ASSEMBLY DO PIC18F4550		   *
;*			AUTOR: MARISMAR COSTA                      *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
    
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                     ARQUIVOS DE DEFINIÇÕES                      *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *	
  LIST p=18f4550, r=hex  
#INCLUDE <p18f4550.inc>		;ARQUIVO PADRÃO MICROCHIP PARA 18F4550
    
; CONFIG1H
  CONFIG    FOSC    = INTOSCIO_EC   ; OSCILLATOR SELECTION BITS (INTERNAL OSCILLATOR, PORT FUNCTION ON RA6, EC USED BY USB (INTIO))
  CONFIG    LVP	    = OFF	    ; SINGLE-SUPPLY ICSP ENABLE BIT (SINGLE-SUPLY ICSP DISABLED) 
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                         VARIÁVEIS                               *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; DEFINIÇÃO DOS NOMES E ENDEREÇOS DE TODAS AS VARIÁVEIS UTILIZADAS 
; PELO SISTEMA

	CBLOCK	0x10		;ENDEREÇO INICIAL DA MEMÓRIA DE USUÁRIO
		W_TEMP		;REGISTRADORES TEMPORÁRIOS PARA USO
		STATUS_TEMP	;JUNTO ÀS INTERRUPÇÕES
		
		;VARIÁVEIS GERAIS
		THETA		;ANGULO DO PRIMEIRO SERVO MOTOR	
		PHI		;ANGULO DO SEGUNDO SERVO MOTOR	
		MELHOR_THETA	;MELHOR ÂNGULO TETA(SERVO MOTOR 1)
		MELHOR_PHI	;MELHOR ÂNGULO PHI(SERVO MOTOR 2)
		V_MAX_THETA	;MÁXIMA TENSÃO EM THETA
		V_MAX_PHI	;MÁXIMA TENSÃO EM PHI
		ANGULO_ATIVO	;ÂNGULO QUE ESTÁ SENDO AJUSTADO, 0 - TETA / 1 - PHI
		
		;VARIÁVEIS AUXILIARES PARA GERAÇÃO DE DELAY
		CONTADOR_DELAY	;AUXILIAR NA REPETIÇÃO DO DELAY DE 10 MICROSEGUNDOS
		FIM_PERIODO	;SINALIZA QUANDO O PERIODO DE 20 MILISEGUNDOS 
		CONTADOR_AUX    ;PARA DELAY DE COMPENSAÇÃO DA VELOCIDADE MÁXIMA DO SERVO
		DELAY1		
		DELAY2
		DELAY3

	ENDC			;FIM DO BLOCO DE MEMÓRIA

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                        FLAGS INTERNOS                           *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; DEFINIÇÃO DE TODOS OS FLAGS UTILIZADOS PELO SISTEMA
	
	#DEFINE	FIM_PERIODO_FLAG    FIM_PERIODO,0   ;FIM_PERIODO = 0 - NÃO ACABOU / 1 - ACABOU
	#DEFINE	ANGULO_ATIVO_FLAG   ANGULO_ATIVO,0  ;ANGULO_ATIVO = 0 - THETA(MOTOR1) / 1 - PHI(MOTOR2)

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                         CONSTANTES                              *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; DEFINIÇÃO DE TODAS AS CONSTANTES UTILIZADAS PELO SISTEMA

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                           ENTRADAS                              *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; DEFINIÇÃO DE TODOS OS PINOS QUE SERÃO UTILIZADOS COMO ENTRADA
; RECOMENDAMOS TAMBÉM COMENTAR O SIGNIFICADO DE SEUS ESTADOS (0 E 1)

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                           SAÍDAS                                *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; DEFINIÇÃO DE TODOS OS PINOS QUE SERÃO UTILIZADOS COMO SAÍDA
; RECOMENDAMOS TAMBÉM COMENTAR O SIGNIFICADO DE SEUS ESTADOS (0 E 1)	
	
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*			      VETORES                              *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

	ORG 0x0000		;ENDEREÇO INICIAL DO PROGRAMA
	GOTO INICIO
	
	ORG 0x0008		;ENDEREÇO DA INTERRUPÇÃO DE ALTA PRIORIDADE
	GOTO HIGH_INT
    
	ORG 0x0018		;ENDEREÇO DA INTERRUPÇÃO DE BAIXA PRIORIDADE
	GOTO LOW_INT
    
    
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*            INÍCIO DA INTERRUPÇÃO DE ALTA PRIORIDADE             *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; ENDEREÇO DE DESVIO DAS INTERRUPÇÕES. A PRIMEIRA TAREFA É SALVAR OS
; VALORES DE "W" E "STATUS" PARA RECUPERAÇÃO FUTURA

HIGH_INT:
	MOVWF	W_TEMP		    ;COPIA W PARA W_TEMP
	SWAPF	STATUS,W
	MOVWF	STATUS_TEMP	    ;COPIA STATUS PARA STATUS_TEMP

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*            ROTINA DE INTERRUPÇÃO DE ALTA PRIORIDADE             *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; AQUI SERÃO ESCRITAS AS ROTINAS DE RECONHECIMENTO E TRATAMENTO DAS
; INTERRUPÇÕES
	BTFSC	LATA,3		    ;TESTA SE O AJUSTE DA POSIÇÃO JÁ FOI FEITO
	GOTO	END_INT
	BSF	ADCON0,1	    ;DISPARA A CONVERSAO A/D
FIM_CONVERSAO			    ;ESPERA A CONVERSAO SER FINALIZADA
	BTFSC	ADCON0,1	    ;VERIFICA SE O FIM DA CONVERSAO
	GOTO	FIM_CONVERSAO	
	BTFSC	ANGULO_ATIVO_FLAG   ;TESTAR SE O ANGULO_ATIVO É PHI OU THETA
	GOTO	CONVERSAO_PHI	    ;ANGULO_ATIVO = 0 - THETA / 1 - PHI
CONVERSAO_THETA
	MOVF	ADRESH,0	    ;MOVE O VALOR DA CONVERSAO PARA WREG
	CPFSLT	V_MAX_THETA	    ;TESTA SE O NOVO VALOR CONVERTIDO É MAIOR QUE O MAIOR ANTERIORMENTE SALVO E DESVIA 
	GOTO	ANGULO_THETA	    ;SE NÃO, DESVIO PARA O TRATAMENTO DO ANGULO THETA
	MOVWF	V_MAX_THETA	    ;ATUALIZA O MAIOR VALOR
	MOVFF	THETA,MELHOR_THETA  ;ATUALIZA O MELHOR ANGULO
	GOTO	ANGULO_THETA
	
CONVERSAO_PHI
	MOVF	ADRESH,0	    ;MOVE O VALOR DA CONVERSAO PARA WREG
	CPFSLT	V_MAX_PHI	    ;TESTA SE O NOVO VALOR CONVERTIDO É MAIOR QUE O MAIOR ANTERIORMENTE SALVO E DESVIA 
	GOTO	ANGULO_PHI	    ;SE NÃO, DESVIO PARA O TRATAMENTO DO ANGULO THETA
	MOVWF	V_MAX_PHI	    ;ATUALIZA O MAIOR VALOR
	MOVFF	PHI,MELHOR_PHI  ;ATUALIZA O MELHOR ANGULO
	GOTO	ANGULO_PHI
	
;	BTFSC	ANGULO_ATIVO_FLAG   ;TESTAR SE O ANGULO_ATIVO É PHI OU THETA
;	GOTO	ANGULO_PHI	    ;ANGULO_ATIVO = 0 - THETA / 1 - PHI
	
ANGULO_THETA
	CLRF	WREG		    ;TESTAR SE O ANGULO É ZERO, SE FOR, ESPERA UM DELAY DE 400 MICROSEGUNDOS
	SUBWF	THETA,0		    ;VERIFICA SE O ANGULO É ZERO	
    	BTFSC	STATUS,Z	    
	GOTO	THETA_ZERO	    ;SE FOR ZERO, DESVIA PARA O DELAY ADICIONAL
INCREMENTA_THETA
	MOVLW	.40
	MOVWF	CONTADOR_AUX	    ;INICIALIZA O CONTADOR
	INCF	THETA		    ;SE NÃO, INCREMENTA O ÂNGULO THETA
	MOVF	THETA,0		    ;COPIA O VALOR DE THETA PARA WORK
	SUBLW	.181		
	BTFSS	STATUS,Z	    ;TESTA SE THETA FOI INCREMENTADO A 181 GRAUS
	GOTO	END_INT		    ;SE THETA < 181, ENTÃO DESVIA PARA O FIM DA INTERRUPÇÃO 
	CLRF	THETA		    ;SE THETA FOR 181, ENTAO THETA RETORNA A 0 GRAUS
	MOVLW	.1
	MOVWF	ANGULO_ATIVO	    ;ALTERA O ANGULO_ATIVO PARA PHI
	GOTO	END_INT
	
ANGULO_PHI
	CLRF	WREG		    ;TESTAR SE O ANGULO É ZERO, SE FOR, ESPERA UM DELAY DE 400 MICROSEGUNDOS
	SUBWF	PHI,0		    ;VERIFICA SE O ANGULO É ZERO	
    	BTFSC	STATUS,Z	    
	GOTO	PHI_ZERO	    ;SE FOR ZERO, DESVIA PARA O DELAY ADICIONAL
INCREMENTA_PHI
	MOVLW	.40
	MOVWF	CONTADOR_AUX	    ;INICIALIZA O CONTADOR
	INCF	PHI		    ;INCREMENTA O ÂNGULO PHI
	MOVF	PHI,0		    ;COPIA O VALOR DE PHI PARA WORK
	SUBLW	.181		
	BTFSS	STATUS,Z	    ;TESTA SE PHI FOI INCREMENTADO A 181 GRAUS
	GOTO	END_INT		    ;SE PHI < 181, ENTÃO DESVIA PARA O FIM DA INTERRUPÇÃO
	;CLRF	PHI		    ;SE PHI FOR 181, ENTAO PHI RETORNA A 0 GRAUS
	MOVFF	MELHOR_PHI,PHI	    
	MOVFF	MELHOR_THETA,THETA
	BSF	LATA,3		    ;SINALIZA QUE ACABOU O AJUSTE DA POSICAO
	CLRF	ANGULO_ATIVO	    ;ALTERA O ANGULO_ATIVO PARA THETA
	GOTO	END_INT
	
THETA_ZERO
	DECFSZ	CONTADOR_AUX	    ;DECREMENTA O CONTADOR ATÉ ATINGIR ZERO
	GOTO	END_INT
	GOTO	INCREMENTA_THETA
	
PHI_ZERO   
	DECFSZ	CONTADOR_AUX	    ;DECREMENTA O CONTADOR ATÉ ATINGIR ZERO
	GOTO	END_INT
	GOTO	INCREMENTA_PHI
	
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*       ROTINA DE SAÍDA DA INTERRUPÇÃO DE ALTA PRIORIDADE         *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; OS VALORES DE "W" E "STATUS" DEVEM SER RECUPERADOS ANTES DE 
; RETORNAR DA INTERRUPÇÃO

END_INT:
	BCF	INTCON,TMR0IF	    ;INTERRUPÇÃO POR TIMER0, LIMPA A FLAG
	BSF	FIM_PERIODO_FLAG    ;SETA O SINALIZADOR DO FIM DO PERIODO DE 20 MILISEGUNDOS
	SWAPF	STATUS_TEMP,W
	MOVWF	STATUS		    ;MOVE STATUS_TEMP PARA STATUS
	SWAPF	W_TEMP,F
	SWAPF	W_TEMP,W	    ;MOVE W_TEMP PARA W
	RETFIE
    
	
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*            INÍCIO DA INTERRUPÇÃO DE BAIXA PRIORIDADE            *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; ENDEREÇO DE DESVIO DAS INTERRUPÇÕES. A PRIMEIRA TAREFA É SALVAR OS
; VALORES DE "W" E "STATUS" PARA RECUPERAÇÃO FUTURA
	
LOW_INT:
	MOVWF	W_TEMP		;COPIA W PARA W_TEMP
	SWAPF	STATUS,W
	MOVWF	STATUS_TEMP	;COPIA STATUS PARA STATUS_TEMP

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*           ROTINA DE INTERRUPÇÃO DE BAIXA PRIORIDADE             *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; AQUI SERÃO ESCRITAS AS ROTINAS DE RECONHECIMENTO E TRATAMENTO DAS
; INTERRUPÇÕES
	
	NOP
	
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*      ROTINA DE SAÍDA DA INTERRUPÇÃO DE BAIXA PRIORIDADE         *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; OS VALORES DE "W" E "STATUS" DEVEM SER RECUPERADOS ANTES DE 
; RETORNAR DA INTERRUPÇÃO
	
	SWAPF	STATUS_TEMP,W
	MOVWF	STATUS		;MOVE STATUS_TEMP PARA STATUS
	SWAPF	W_TEMP,F
	SWAPF	W_TEMP,W	;MOVE W_TEMP PARA W
	RETFIE
    
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*	            	 ROTINAS E SUBROTINAS                      *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; CADA ROTINA OU SUBROTINA DEVE POSSUIR A DESCRIÇÃO DE FUNCIONAMENTO
; E UM NOME COERENTE ÀS SUAS FUNÇÕES.

DELAY_600U
	;CORPO DA ROTINA1
	CLRF	DELAY1
	MOVLW	.10		;VALOR INICIAL DO DELAY2
	MOVWF	DELAY2
LOOP1
	DECFSZ	DELAY1		
	GOTO	LOOP1
	DECFSZ	DELAY2
	GOTO	LOOP1
	RETURN
	
	
DELAY_AUXILIAR
	;CORPO DA ROTINA2
	CLRF	WREG		;LIMPA O REGISTRADOR WORK
	SUBWF	CONTADOR_DELAY,0;SE CONTADOR FOR 0, DESVIA PARA O RETORNO DA SUBROTINA
	BTFSC	STATUS,Z	;TESTA O BIT ZERO DO STATUS
	RETURN			;NÃO EXECUTA O DELAY, POIS O ANGULO É 0 
DELAY_10U
	MOVLW	.40		;VALOR INICIAL DO DELAY2
	MOVWF	DELAY3		
LOOP2				;CADA CICLO EQUIVALE A 10USEGUNDOS    
	DECFSZ	DELAY3
	GOTO	LOOP2
	DECFSZ	CONTADOR_DELAY	;REPETE O CICLO VÁRIAS VEZES DE ACORDO COM O ÂNGULO
	GOTO	DELAY_10U
	RETURN

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                     INICIO DO PROGRAMA                          *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
	
INICIO:
	MOVLW	B'00000001'	;TRISA DEFINE AS PORTAS RA<7:0> COMO ENTRADAS OU SAÍDAS
	MOVWF	TRISA		;1 - ENTRADA/ 0 - SAÍDA		
	MOVLW	B'10000001'	;CONFIGURAÇÃO DO TIMER0
	MOVWF	T0CON
	MOVLW	B'11100000'	;CONFIGURAÇÃO DA INTERRUPÇÃO PELO TIMER0
	MOVWF	INTCON
	MOVLW	B'00000100'	;A INTERRUOÇÃO DO TIMER0 É DEFINIDA COMO ALTA
	MOVWF	INTCON2
	MOVLW	B'00001110'	;CONFIGURA RA0 COMO CANAL ANALÓGICO(AN0)
	MOVWF	ADCON1 
	MOVLW	B'00000000'	;MÓDULO COMPARADOR DESATIVADO
	MOVWF	CMCON 
	MOVLW	B'00000110'	;CONFIGURA O FOSC DO CONVERSOR A/D
	MOVWF	ADCON2
	MOVLW	B'00000001'	;HABILITA O CONVERSOR A/D
	MOVWF	ADCON0
	CLRF	PORTA		;LIMPA AS SAÍDAS RA<7:0>
	CLRF	LATA

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                     INICIALIZAÇÃO DAS VARIÁVEIS                 *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
			
	CLRF	THETA		;ÂNGULO INICIAL: 0 GRAUS
	CLRF	PHI		;ÂNGULO INICIAL: 0 GRAUS
	CLRF	ANGULO_ATIVO	;INICIALIZA O PROGRAMA AJUSTANDO O THETA(MOTOR1)
	CLRF	FIM_PERIODO	;FIM_PERIODO = 0
	CLRF	CONTADOR_DELAY	;INICIALIZA COMO ZERO 
	MOVLW	.40
	MOVWF	CONTADOR_AUX	;INICIALIZA EM 40
	
	
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                     ROTINA PRINCIPAL                            *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
MAIN
	;CORPO DA ROTINA PRINCIPAL
	BTFSC	ANGULO_ATIVO_FLAG   ;VERIFICA QUAL O ANGULO DEVE SER AJUSTADO	
	GOTO	ALTERA_PHI	    ;SE MOTOR_ATIVO = 1, ANGULO_ATIVO = PHI
	
	BSF	LATA,1		    ;SE MOTOR_ATIVO = 0, ANGULO_ATIVO = THETA
	BCF	FIM_PERIODO_FLAG    ;LIMPA O SINALIZADOR DO TÉRMINO DO PERÍODO
	CALL	DELAY_600U	    ;PERMANECE POR 600USEGUNDOS
	MOVFF	THETA,CONTADOR_DELAY;MAIS THETA*10USEGUNDOS EM NÍVEL ALTO
	CALL	DELAY_AUXILIAR	    ;CONTADOR É INICIALIZADO COM O VALOR DO ÂNGULO
	BCF	LATA,1		    ;ATÉ O PERÍODO TERMINAR, RA1 = 0	
	GOTO	TERMINOU_PERIODO    ;ESPERA O PERÍODO DE 20 MILISEGUNDOS ACABAR
	
ALTERA_PHI
	BSF	LATA,2		    ;RA2 EQUIVALE AO SINAL DO SERVO MOTOR 2, PHI
	BCF	FIM_PERIODO_FLAG    ;LIMPA O SINALIZADOR DO TÉRMINO DO PERÍODO
	CALL	DELAY_600U	    ;PERMANECE POR 600USEGUNDOS
	MOVFF	PHI,CONTADOR_DELAY  ;MAIS PHI*10USEGUNDOS EM NÍVEL ALTO
	CALL	DELAY_AUXILIAR	    ;CONTADOR É INICIALIZADO COM O VALOR DO ÂNGULO
	BCF	LATA,2		    ;ATÉ O PERÍODO TERMINAR, RA2 = 0	
	
TERMINOU_PERIODO
	BTFSS	FIM_PERIODO_FLAG    ;FIM_PERIODO = 0 - NÃO ACABOU / 1 - ACABOU
	GOTO	TERMINOU_PERIODO    ;AGUARDA ATÉ O FIM DO PERÍODO DE 20MSEGUNDOS
	GOTO	MAIN		    ;ACABOU, RETORNA AO MAIN


;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                       FIM DO PROGRAMA                           *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

	END